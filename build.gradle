buildscript {
    ext {
        //general
        kotlin_version = '1.3.70'
        serialization_version = '0.20.0'

        junit_version = "4.12"
        jacoco_version = "0.8.4"

        //mnid module
        kethereum_version = "0.81.4"
        khash_version = "1.0.0-RC5"
        khex_version = "1.0.0-RC6"
        kbase58_version = "0.1"

        //testing
        test_runner_version = "1.0.2"

        current_release_version = "0.4.3"
    }

    repositories {
        jcenter()
        google()
    }

    dependencies {
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$kotlin_version"
        classpath 'com.android.tools.build:gradle:3.5.3'
    }
}


allprojects {

    apply plugin: 'jacoco'

    jacoco {
        toolVersion = jacoco_version
    }

    repositories {
        jcenter()
        google()
        maven { url 'https://jitpack.io' }
    }

}

subprojects { subproject ->
    afterEvaluate {
        subproject.tasks.withType(org.jetbrains.kotlin.gradle.tasks.KotlinCompile).all {
            kotlinOptions {
                jvmTarget = "1.8"
            }
        }
    }
}

apply from: "publishing.gradle"

task jacocoReport(type: JacocoReport) {
    subprojects { sub ->
        afterEvaluate {
            //subproject tasks can only be gathered after evaluation
            def testTasks = sub.tasks.withType(Test)
            dependsOn testTasks
            executionData testTasks
        }
        classDirectories.from(file("${sub.buildDir}/classes/java/main"))
        classDirectories.from(file("${sub.buildDir}/classes/kotlin/main"))
    }

    reports {
        html.enabled = true
        xml.enabled = true
        csv.enabled = false
    }
}
